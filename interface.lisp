(in-package :graph-db)

(defgeneric copy (node)
  (:documentation
   "Return a mutable copy of vertex or edge NODE, registered with the current
transaction.  This is the first step of the copy-modify-save update pattern:
copy the node inside a WITH-TRANSACTION, SETF its slots, then SAVE the copy.")
  (:method (thing)
    (error "Cannot save ~S of type ~S" thing (type-of thing)))
  (:method ((vertex vertex))
    (copy-vertex vertex))
  (:method ((edge edge))
    (copy-edge edge)))

(defgeneric mark-deleted (node)
  (:documentation
   "Soft-delete vertex or edge NODE: set its deleted flag so it no longer
appears in queries, adjacency, or type scans (it remains on disk).  Wraps a
transaction automatically.  This is the standard way to delete a node.")
  (:method (thing)
    (error "Cannot delete ~S of type ~S" thing (type-of thing)))
  (:method ((vertex vertex))
    (delete-vertex vertex))
  (:method ((edge edge))
    (delete-edge edge)))

(defgeneric save (object &key graph)
  (:documentation
   "Persist OBJECT (a vertex or edge copy) to GRAPH within the current
transaction; the final step of the copy-modify-save update pattern.  OBJECT
must have been produced by COPY in this transaction.  See UPDATE-NODE.")
  (:method (thing &key graph)
    (declare (ignore graph))
    (error "Cannot save ~S of type ~S" thing (type-of thing)))
  (:method ((vertex vertex) &key (graph *graph*))
    (update-node vertex graph))
  (:method ((edge edge) &key (graph *graph*))
    (update-node edge graph)))

